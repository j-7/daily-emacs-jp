#+BLOG: rubikitch
#+POSTID: 843
#+BLOG: rubikitch
#+DATE: [2015-04-13 Mon 07:26]
#+PERMALINK: firestarter
#+OPTIONS: toc:nil num:nil todo:nil pri:nil tags:nil ^:nil \n:t -:nil
#+ISPAGE: nil
#+DESCRIPTION:
# (progn (erase-buffer)(find-file-hook--org2blog/wp-mode))
#+BLOG: rubikitch
#+CATEGORY: プログラム実行
#+EL_PKG_NAME: firestarter
#+TAGS: ファイルローカル変数
#+EL_TITLE0: ファイル保存後に自動でシェルコマンド・Emacsコマンド・S式を実行させる「安全な」設定を個別ファイルに指定する
#+EL_URL: 
#+begin: org2blog
#+TITLE: firestarter.el : ファイル保存後に自動でシェルコマンド・Emacsコマンド・S式を実行させる「安全な」設定を個別ファイルに指定する
[includeme file="inc-package-table.php" name="firestarter" where="melpa"]

#+end:
** 概要
ファイル保存後に特定のアクションをさせたいことはよくあります。

昨日紹介した[[http://emacs.rubikitch.com/auto-shell-command-2/][auto-shell-command.el(レビュー)]] もそのひとつです。

auto-shell-command.elはinit.elにまとめて書く必要がある上に、
シェルコマンドの実行しかサポートされていません。

今回紹介する *firestarter.el* は、ファイルローカル変数や
ディレクトリローカル変数に保存後の処理を記述する方式です。

使い方は簡単で、ファイル・ディレクトリローカル変数 *firestarter* に
値を設定するだけです。

- 文字列: 実行させるシェルコマンド
- シンボル: 実行させるEmacsコマンド
- S式: 実行させるS式

シェルコマンドには以下の%記法が使えます。

#+BEGIN_SRC emacs-lisp :results silent
;;; firestarter-formatより
(let* ((buffer (buffer-name))           ;%b バッファ名
       (path (or (buffer-file-name) "")) ;%p フルパス
       (file (file-name-nondirectory (or path ""))) ;%f ファイル名(ディレクトリ除外)
       (stem (file-name-sans-extension file)) ;%s 拡張子を抜いたフルパス
       (extension (file-name-extension file t))) ;%e 拡張子
  (format-spec string (format-spec-make ?b buffer ?p path ?f file
                                        ?s stem ?e extension)))
#+END_SRC

シンボル…たとえば「firestarter: gomoku」とすれば
保存時に五目並べをします(意味ねぇw)

S式は「firestarter: (message "ok")」などと指定します。

[includeme file="inc-package-install.php" name="firestarter" where="melpa"]
** セキュリティと利便性の兼ね合い
ファイル・ディレクトリローカル変数というのは本来危険と隣り合わせです。

悪意のある人がシステムを壊滅させる設定をさせることができるからです。

そのためEmacs側にセーフティガードが設けられていて、
変数を有効にするか訊いてきます。

ここで!を押せば安全な値として恒久的に有効にできますし、
nを押せば拒否できます。

# (progn (forward-line 1)(shell-command "screenshot-time.rb org_template" t))
#+ATTR_HTML: :width 480
[[file:/r/sync/screenshots/20150413073345.png]]
Fig1: _

とはいえ毎回それを訊かれるのはうざったいです。

安全性と利便性というのはしばしばトレードオフになります。

今回、両者をうまくとりまとめるため、以下の方針にしています。
- firestarterに任意の値を受け付けるようにする
- ファイルを開いたときにfirestarterの値を示す
- ファイルに関連付けられていないバッファ保存時に警告

任意の値を受け付けるのは本当に危険なので、
毎回訊かれる代わりにfirestarterを意識させるようにします。


# (progn (forward-line 1)(shell-command "screenshot-time.rb org_template" t))
#+ATTR_HTML: :width 480
[[file:/r/sync/screenshots/20150413080907.png]]
Fig2: ファイルを開いたらfirestarterの設定をお知らせ

#+ATTR_HTML: :width 480
[[file:/r/sync/screenshots/20150413080914.png]]
Fig3: 実行したことをお知らせ



** 設定 [[http://rubikitch.com/f/150413064753.firestarter.1.el][150413064753.firestarter.1.el(以下のコードと同一)]]
#+BEGIN: include :file "/r/sync/junk/150413/150413064753.firestarter.1.el"
#+BEGIN_SRC fundamental
(firestarter-mode)
;;; 失敗時に結果をお知らせ
(setq firestarter-default-type 'failure)
;;; コマンド実行後に実行したコマンドを知らせる
(defun firestarter-sentinel--show-command (&rest ignore)
  (run-at-time 1 nil 'message (format "firestarted: %s" (firestarter-format firestarter))))
(advice-add 'firestarter-sentinel :before 'firestarter-sentinel--show-command)

;;; セキュリティと利便性を両立
;; firestarterに任意の値を受け取れるようにする
(put 'firestarter 'safe-local-variable 'identity)
;; firestarterが設定されているときはmessageでお知らせ
(defun find-file-hook--firestarter-notify ()
  (when (and (bound-and-true-p firestarter-mode) firestarter)
    (run-at-time 1 nil 'message "firestarter = %S" firestarter)))
(add-hook 'find-file-hook 'find-file-hook--firestarter-notify)
;; ファイルに関連付けられていないバッファの保存時にfirestarterが設定されているときに警告
(defun warn-firestarter-before-saving-nonfile-buffer (&rest ignore)
  (let (it)
    (when (and (not buffer-file-name)
               (save-excursion
                 (save-restriction
                   (widen)
                   (goto-char (point-min))
                   (and (re-search-forward "firestarter *:" nil t)
                        (setq it (buffer-substring (point-at-bol) (point-at-eol))))))
               (not (yes-or-no-p (concat "Save buffer with firestarter\n" it))))
      (error "Quit saving because of dangerous firestarter setting."))))
(advice-add 'basic-save-buffer :before 'warn-firestarter-before-saving-nonfile-buffer)
(advice-add 'write-file :before 'warn-firestarter-before-saving-nonfile-buffer)

#+END_SRC

#+END:

** 実行方法
#+BEGIN_EXAMPLE
$ wget http://rubikitch.com/f/150413064753.firestarter.1.el
$ emacs -Q -f package-initialize -l 150413064753.firestarter.1.el
#+END_EXAMPLE

# /r/sync/screenshots/20150413073345.png http://rubikitch.com/wp-content/uploads/2015/04/wpid-20150413073345.png
# /r/sync/screenshots/20150413080907.png http://rubikitch.com/wp-content/uploads/2015/04/wpid-20150413080907.png
# /r/sync/screenshots/20150413080914.png http://rubikitch.com/wp-content/uploads/2015/04/wpid-20150413080914.png
[includeme file="inc-package-relate.php" name="firestarter"]
